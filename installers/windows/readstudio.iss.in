;Installer for ${PROGRAM_LONGNAME}
[Setup]
#include "setup.isl"
MergeDuplicateFiles=true
Compression=lzma
SolidCompression=true
UserInfoPage=true
ChangesAssociations=true
PrivilegesRequired=admin
LicenseFile=..\..\LICENSE
ShowLanguageDialog=yes
LanguageDetectionMethod=locale
AppendDefaultDirName=no
ArchitecturesAllowed=x64
ArchitecturesInstallIn64BitMode=x64
; Don't use MinVersion because some customized Windows systems
; have their OS version messed up and InnoSetup can't determine the version.

[Files]
; program files
Source: release\${CMAKE_PROJECT_NAME}.exe; DestDir: {app}; Components: ProgramFiles; Flags: replacesameversion restartreplace
; resource files (required part of programs)
Source: resources\res.wad; DestDir: {app}; Components: ProgramFiles; Flags: replacesameversion restartreplace recursesubdirs
Source: resources\words.wad; DestDir: {app}; Components: ProgramFiles; Flags: replacesameversion restartreplace recursesubdirs
Source: ..\..\resources\scripting\*.api; DestDir: {app}; Components: ProgramFiles; Flags: replacesameversion restartreplace recursesubdirs
Source: ..\..\resources\scripting\*.lua; DestDir: {app}; Components: ProgramFiles; Flags: replacesameversion restartreplace recursesubdirs
Source: ..\..\resources\report-themes\*; DestDir: {app}\report-themes; Components: ProgramFiles; Flags: replacesameversion restartreplace recursesubdirs
Source: ..\..\LICENSE; DestDir: {app}; Components: ProgramFiles; Flags: replacesameversion
Source: ..\..\citations.bib; DestDir: {app}; Components: ProgramFiles; Flags: replacesameversion
Source: ..\..\citations.mla; DestDir: {app}; Components: ProgramFiles; Flags: replacesameversion
Source: ..\..\citations.apa; DestDir: {app}; Components: ProgramFiles; Flags: replacesameversion
; help files
Source: ..\..\docs\readability-studio-manual\docs-manual\*; DestDir: {app}\readability-studio-manual; Components: HelpFiles; Excludes: "thumbs.db,*.tex,*.log,README.md,\.git"; Flags: replacesameversion restartreplace recursesubdirs
Source: ..\..\docs\readability-studio-manual\docs-online\*; DestDir: {app}\readability-studio-manual\online; Components: HelpFiles; Excludes: "thumbs.db,*.tex,*.log,README.md,\.git"; Flags: replacesameversion restartreplace recursesubdirs
Source: ..\..\docs\readability-test-reference\docs\readability-test-reference.pdf; DestDir: {app}\readability-studio-manual; Components: HelpFiles; Flags: replacesameversion restartreplace recursesubdirs
Source: ..\..\docs\shortcuts-cheatsheet\docs\shortcuts-cheatsheet.pdf; DestDir: {app}\readability-studio-manual; Components: HelpFiles; Flags: replacesameversion restartreplace recursesubdirs
Source: ..\..\docs\sysadmin\docs\sysadmin-manual.pdf; DestDir: {app}\readability-studio-manual; Components: HelpFiles; Flags: replacesameversion restartreplace recursesubdirs
Source: ..\..\docs\release-notes\docs\release-notes.pdf; DestDir: {app}\readability-studio-manual; Components: HelpFiles; Flags: replacesameversion restartreplace recursesubdirs
Source: ..\..\docs\readability-studio-api\docs\readability-studio-api.pdf; DestDir: {app}\readability-studio-manual; Components: HelpFiles; Flags: replacesameversion restartreplace recursesubdirs
Source: ..\..\docs\lua-5.4\doc\*; DestDir: {app}\readability-studio-manual\lua-5.4\doc; Components: HelpFiles; Excludes: "thumbs.db"; Flags: replacesameversion restartreplace recursesubdirs
; examples
Source: ..\..\examples\*; DestDir: {app}\examples; Components: Examples; Excludes: "thumbs.db,README.md,\.git"; Flags: replacesameversion restartreplace recursesubdirs
; translations
Source: resources\es\*.mo; DestDir: {app}; Components: Translations; Flags: replacesameversion restartreplace recursesubdirs

[Languages]
#include "lang.isl"

[Icons]
#include "icons.isl"

[Registry]
; Make is so that you can run the program from the command line
Root: HKLM; Subkey: SOFTWARE\Microsoft\Windows\CurrentVersion\App Paths\${CMAKE_PROJECT_NAME}.exe; ValueType: string; ValueName: ; ValueData: {app}\${CMAKE_PROJECT_NAME}.exe; Flags: uninsdeletekey
; Set file association (standard project)
Root: HKCR; Subkey: .rsp; ValueType: string; ValueData: ReadStudioProject; Flags: uninsdeletekey
Root: HKCR; Subkey: ReadStudioProject; ValueType: string; ValueName: ; ValueData: ${readstudio_DESCRIPTION} Project; Flags: uninsdeletekey
Root: HKCR; Subkey: ReadStudioProject\DefaultIcon; ValueType: string; ValueData: {app}\${CMAKE_PROJECT_NAME}.EXE,0; Flags: uninsdeletekey
Root: HKCR; Subkey: ReadStudioProject\shell\open\command; ValueType: string; ValueData: """{app}\${CMAKE_PROJECT_NAME}.EXE"" ""%1"""; Flags: uninsdeletekey
; Set file association (batch project)
Root: HKCR; Subkey: .rsbp; ValueType: string; ValueData: ReadStudioBatchProject; Flags: uninsdeletekey
Root: HKCR; Subkey: ReadStudioBatchProject; ValueType: string; ValueName: ; ValueData: ${readstudio_DESCRIPTION} Project; Flags: uninsdeletekey
Root: HKCR; Subkey: ReadStudioBatchProject\DefaultIcon; ValueType: string; ValueData: {app}\${CMAKE_PROJECT_NAME}.EXE,0; Flags: uninsdeletekey
Root: HKCR; Subkey: ReadStudioBatchProject\shell\open\command; ValueType: string; ValueData: """{app}\${CMAKE_PROJECT_NAME}.EXE"" ""%1"""; Flags: uninsdeletekey

[Components]
Name: ProgramFiles; Description: {cm:ProgramFiles}; Flags: fixed; Types: Typical Custom
Name: HelpFiles; Description: {cm:HelpFiles}; Types: Typical Custom
Name: Examples; Description: {cm:Examples}; Types: Typical Custom
Name: Translations; Description: {cm:Translations}; Types: Typical Custom

[Types]
Name: Typical; Description: {cm:Typical}
Name: Custom; Description: {cm:Custom}; Flags: iscustom

[InstallDelete]
; Delete any old start menu icons
Type: filesandordirs ; Name:"{commonprograms}\${readstudio_DESCRIPTION}"

[UninstallDelete]
Type: files; Name: {app}\rs.xml
Name: {app}\res; Type: dirifempty
Name: {app}; Type: dirifempty

[Code]

var
  Upgrade: Boolean;

function InitializeSetup(): Boolean;
var
  userName: String;
begin
  // next, see if this is an upgrade installation
  if RegQueryStringValue(HKLM,
    'Software\Microsoft\Windows\CurrentVersion\Uninstall\${INNOSETUP_GUID}_is1',
    'Inno Setup: User Info: Name', userName) then
    begin
      Upgrade := true
    end
  // no user info, so prompt for it later
  else
    Upgrade := false;

  Result := true
end;

// see if certain dialogs should be skipped if we are in upgrade mode
function ShouldSkipPage(PageID: Integer): Boolean;
var
   iconGroup: String;
   installDir: String;
begin
  if (PageID = wpUserInfo) then
    Result := Upgrade
  else if (PageID = wpLicense) then
    Result := Upgrade
  else if (PageID = wpReady) then
    Result := Upgrade
  // install directory dialog
  else if (PageID = wpSelectDir) then
    begin
      if (Upgrade = true) then
        begin
          // first, see if there is an installation folder already
          if RegQueryStringValue(HKLM,
              'Software\Microsoft\Windows\CurrentVersion\Uninstall\${INNOSETUP_GUID}_is1',
              'Inno Setup: App Path', installDir) then
            Result := true
          // no path found, so prompt for it
          else
            Result := false
        end
      else
        Result := false
    end
  // menu group dialog
  else if (PageID = wpSelectProgramGroup) then
    begin
      if (Upgrade = true) then
        begin
          // first, see if there is a menu group already
          if RegQueryStringValue(HKLM,
              'Software\Microsoft\Windows\CurrentVersion\Uninstall\${INNOSETUP_GUID}_is1',
              'Inno Setup: Icon Group', iconGroup) then
            Result := true
          // no menu group found, so prompt for it
          else
            Result := false
        end
      else
        Result := false
    end
  else
    Result := false
end;
